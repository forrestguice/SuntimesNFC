plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.forrestguice.suntimes.nfc"
        minSdkVersion 16
        //noinspection OldTargetApi
        targetSdkVersion 33
        versionCode 3
        versionName "0.1.2"

        manifestPlaceholders = [suntimesApplicationId:"com.forrestguice.suntimeswidget", suntimesPermissionRoot:"suntimes"]
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        missingDimensionStrategy 'version', 'androidx'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "versiontype"
    productFlavors {
        production {
            dimension "versiontype"
            getIsDefault().set(true)
        }
        nightly {
            dimension "versiontype"
            applicationIdSuffix ".nightly"
            versionNameSuffix "-nightly"
            def authorityRoot = "suntimes.intervalmidpoints.nightly"
            manifestPlaceholders = [suntimesApplicationId:"com.forrestguice.suntimeswidget", suntimesPermissionRoot:"suntimes"]
            buildConfigField "String", "AUTHORITY_ROOT", "\"$authorityRoot\""
            def nightlyVersionCode = new Date().format('yyyyMMddhh') as int
            versionCode nightlyVersionCode
        }
    }

    lintOptions {
        disable 'MissingTranslation'//,'WrongRegion','ObsoleteSdkInt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    android.defaultConfig.vectorDrawables.useSupportLibrary = true
}

dependencies {
    //noinspection GradleDependency
    implementation 'com.google.android.material:material:1.6.1'
    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.6.1'
    //noinspection GradleDependency
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation project(path: ":SuntimesAddon")  //implementation 'com.github.forrestguice:SuntimesAddon:v0.4.3'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

def getGitHash()
{
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}